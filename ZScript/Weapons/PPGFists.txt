
Class PPGFists : PPGWeapon
{
	Default {
		Weapon.SelectionOrder 3700;
		Inventory.Icon "FISTICON";
		+WEAPON.NOALERT
		+NOEXTREMEDEATH
		Obituary "%o got brawled by %k.";
	}
   States
   {
	Deselect:
		TNT1 A 0 {
			A_Overlay(FistsPLA,"Null");
			A_Overlay(FistsPRA,"Null");
			A_Overlay(FistsPLB,"Null");
			A_Overlay(FistsPRB,"Null");
			A_Overlay(FistsL,"Null");
			A_Overlay(FistsR,"Null");
			A_PlaySound("fists/raise",CHAN_AUTO);
		}
	Lower:
		TNT1 A 0 { 
			A_TakeInventory("RHSeq",9999);
			A_TakeInventory("PPGFistsPunchLeft",9999);
			A_TakeInventory("FistsInUse",9999);
			A_TakeInventory("WeaponInUse",9999);
			A_TakeInventory("SPInUse",9999);
			A_TakeInventory("SP6InUse",9999);
			A_TakeInventory("SP7InUse",9999);
		}
		FISG A 1 A_Lower(12);
		loop;
	Select:
		TNT1 A 0 {
			A_Overlay(FistsL,"FistsLeftReady");
			A_Overlay(FistsR,"FistsRightReady");
			A_OverlayFlags(FistsL,PPGWeapMove,true);
			A_OverlayFlags(FistsR,PPGWeapMove,true);
			A_PlaySound("fists/raise",CHAN_AUTO);
		}
	Raise:
		TNT1 A 0 {
			A_GiveInventory("FistsInUse",9999);
			A_TakeInventory("RHSeq",9999);
			A_TakeInventory("PPGFistsPunchLeft",9999);
			A_TakeInventory("WeaponInUse",9999);
		 	A_TakeInventory("SPInUse",9999);
			A_TakeInventory("SP6InUse",9999);
			A_TakeInventory("SP7InUse",9999);
		}
		TNT1 A 1 A_Raise(12);
		Loop;
    Ready:
    	TNT1 A 0 {
			if(!CheckInventory("WeaponInUse",1)
				&& !CheckInventory("SP7InUse",1)) {
				if(!CheckInventory("SP6InUse",1)) {
					A_Overlay(FistsL,"FistsLeftReady",true);
				}
				A_Overlay(FistsR,"FistsRightReady",true);
				A_OverlayFlags(FistsL,PPGWeapBob,true);
				A_OverlayFlags(FistsR,PPGWeapBob,true);
			}
		}
    	Goto ReadyLoop;
	ReadyLoop:
    	TNT1 A 1 {
			if(CheckInventory("WeaponInUse",1) || CheckInventory("SPInUse",1)) {
					if(CheckInventory("SP7InUse",1)) {
						A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
					}
					else if(CheckInventory("SPInUse",1)) {
						A_WeaponReady(WRF_NOSWITCH);
					}
					else {
						A_WeaponReady(WRF_ALLOWUSER1|WRF_NOSWITCH);
					}
			}
			else {
				A_WeaponReady(WRF_ALLOWUSER1);	
			}
		}
    	Goto Ready;
   FistsLeftReady:
	  FISL A -1;
	  stop;
   FistsRightReady:
	  FISR A -1;
	  stop;
   Fire:
	  TNT1 A 0 A_Jump(255,"Punch1","Punch2");
	Punch1:
		TNT1 A 0 A_JumpIfInventory("SP6InUse",1,"Punch2");
		TNT1 A 1 { 
			if(!CheckInventory("WeaponInUse",1)
				&& !CheckInventory("SP7InUse",1)) {
					A_GiveInventory("PPGFistsPunchLeft",1);
					A_Overlay(FistsL,"Null");
					A_Overlay(FistsR,"Null");
					A_Overlay(FistsPLA,"PunchLeftA");
					A_Overlay(FistsPLB,"PunchLeftB");
			}
			if(CheckInventory("User1Refire",1)) {
				A_Overlay(SPower,"Superpowers");
			}
		}
		Goto Ready;
	Punch2:
		TNT1 A 1 { 
			if(!CheckInventory("WeaponInUse",1)
				&& !CheckInventory("SP7InUse",1)) {
					A_Overlay(FistsL,"Null");
					A_Overlay(FistsR,"Null");
					A_Overlay(FistsPRA,"PunchRightA");
					if(!CheckInventory("SP6InUse",1)) {
						A_Overlay(FistsPRB,"PunchRightB");
					}
			}
			if(CheckInventory("User1Refire",1)) {
				A_Overlay(SPower,"Superpowers");
			}
		}
		Goto Ready;
	PunchRightA:
	  TNT1 A 0 A_GiveInventory("WeaponInUse",1);
	  FISR A 1 A_PlaySound("fists/swing",CHAN_AUTO);
	  FISR BC 1;
	  FISR D 4 A_CustomPunch(8,1,0,"FistPuff2");
	  FISR CBA 1;
	  TNT1 A 0 {
		if(CheckInventory("WeaponInUse",1)) {
			if(!CheckInventory("SP6InUse",1)) {
				A_Overlay(FistsL,"FistsLeftReady");
			}
			A_Overlay(FistsR,"FistsRightReady");
		}
	  }
	  TNT1 A 0 A_TakeInventory("WeaponInUse",1);
	  Stop;
	PunchRightB:
	  FISL A 1 Offset(0,0);
	  FISL A 1 Offset(-3,5);
	  FISL A 1 Offset(-7,18);
	  FISL A 4 Offset(-14,40);
	  FISL A 1 Offset(-7,18);
	  FISL A 1 Offset(-3,7);
	  FISL A 1 Offset(0,0);
	  TNT1 A 0 {
		if(CheckInventory("WeaponInUse",1)) {
			A_Overlay(FistsL,"FistsLeftReady");
			 A_Overlay(FistsR,"FistsRightReady");
		}
	}
	  Stop;
   PunchLeftA:
	  TNT1 A 0 {
			A_GiveInventory("WeaponInUse",1);
			A_GiveInventory("PPGFistsPunchLeft",1);
	  }
	  FISL A 1 A_PlaySound("fists/swing",CHAN_AUTO);
	  FISL BC 1;
	  FISL D 1 A_CustomPunch(8,1,0,"FistPuff");
	  FISL D 1 Offset(44,8);
	  FISL D 1 Offset(84,68);
	  TNT1 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
	  FISL A 1 Offset(0,30);
	  FISL A 1 Offset(0,15);
	  FISL A 1 Offset(0,0);
	  TNT1 A 0 {
		if(CheckInventory("WeaponInUse",1)) {
			 A_TakeInventory("PPGFistsPunchLeft",1);
			 A_Overlay(FistsL,"FistsLeftReady");
			 A_Overlay(FistsR,"FistsRightReady");
		}
	}
	  TNT1 A 0 A_TakeInventory("WeaponInUse",1);
	  FISL A 1;
	  Stop;
	PunchLeftB:
	  FISR A 1 Offset(0,0);
	  FISR A 1 Offset(3,5);
	  FISR A 1 Offset(7,18);
	  FISR A 4 Offset(14,40);
	  FISR A 1 Offset(0,30);
	  FISR A 1 Offset(0,15);
	  FISR A 1 Offset(0,0);
	  TNT1 A 0 {
		if(CheckInventory("WeaponInUse",1)) {
			 A_Overlay(FistsL,"FistsLeftReady");
			 A_Overlay(FistsR,"FistsRightReady");
		}
	}
	  Stop;
   AltFire:
	  TNT1 A 1 A_JumpIfInventory("WeaponInUse",1,"Ready");
	  TNT1 A 1 A_JumpIfInventory("SPInUse",1,"Ready");
	  TNT1 A 0 {
			A_Overlay(FistsL,"Null");
			A_Overlay(FistsR,"Null");
	  }
      TNT1 A 0 A_GiveInventory("RHSeq",1);
	  FISG A 1 Offset(0,47); //A_WeaponReady(WRF_NOPRIMARY|WRF_NOSWITCH|WRF_NOBOB)
	  FISG A 1 Offset(0,62); //A_WeaponReady(WRF_NOPRIMARY|WRF_NOSWITCH|WRF_NOBOB)
	  TNT1 AAA 2 A_JumpIfInventory("User1Refire",1,"RoundhouseKick");
	  KICK A 1 Offset(6,62) A_PlaySound("fists/swing",CHAN_AUTO);
	  KICK A 1 Offset(3,47);
	  KICK B 4 A_CustomPunch(16,1,0,"FistPuff");
	  KICK A 1 Offset(3,47);
	  KICK A 1 Offset(6,62);
	  TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
	  FISG A 1 Offset(0,62);
	  FISG A 1 Offset(0,47);
	  FISG A 1 Offset(0,32) {
		if(CheckInventory("RHSeq",1)) {
			A_Overlay(FistsL,"FistsLeftReady");
			A_Overlay(FistsR,"FistsRightReady");
		}
		A_TakeInventory("RHSeq",9999);
	  }
	  Goto Ready;
   RoundhouseKick:
	  TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
	  TNT1 A 0 A_PlaySound("roundhouse/swing",CHAN_AUTO);
	  TNT1 AAAA 1 A_SetAngle(angle-40);
	  KICK B 1 Offset(-320,112) A_SetAngle(angle-40);
	  KICK B 1 Offset(-240,80) A_SetAngle(angle-40);
	  KICK B 1 Offset(-160,56) A_SetAngle(angle-40);
	  KICK B 1 Offset(-80,40) A_SetAngle(angle-40);
	  TNT1 A 0 A_SetAngle(angle-40);
	  KICK B 1 Offset(0,32)A_CustomPunch(32,1,0,"RHPuff");
	  KICK B 1 Offset(80,40);
	  KICK B 1 Offset(160,56);
	  KICK B 1 Offset(240,80);
	  KICK B 1 Offset(320,112);
	  TNT1 AAAAAAAA 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
	  FISG A 1 Offset(0,62);
	  FISG A 1 Offset(0,47);
	  FISG A 1 Offset(0,32) {
		if(CheckInventory("RHSeq",1)) {
			 A_Overlay(FistsL,"FistsLeftReady");
			A_Overlay(FistsR,"FistsRightReady");
		}
		A_TakeInventory("RHSeq",9999);
	  }
	  Goto Ready;
	  
   LBRaise:
	  TNT1 A 0 {
			A_GiveInventory("SPInUse",1);
			A_GiveInventory("SP6InUse",1);
		}
	  TNT1 A 0 A_JumpIfInventory("PPGFistsPunchLeft",1,"LBAbort");
	  "####" "#" 1 {
			A_Overlay(FistsPLA,"Null");
			A_Overlay(FistsPRB,"Null");
			A_Overlay(FistsL,"Null");
			A_Overlay(SPower6,"LightningBolt");
		}
	  Stop;
   LBLower:
	  "####" "#" 0 {
			A_Overlay(FistsL,"FistsLeftReady");
			A_TakeInventory("SPInUse",9999);
			A_TakeInventory("SP6InUse",9999);
	  }
	  Stop;
	LBAbort:
	  TNT1 A 0 {
			A_TakeInventory("SPInUse",1);
			A_TakeInventory("SP6InUse",1);
		}
		Stop;
   FBHolster:
	  FISG A 0  {
			A_GiveInventory("SPInUse",1);
			A_GiveInventory("SP7InUse",1);
			 A_Overlay(FistsL,"Null");
			A_Overlay(FistsR,"Null");
		}
	  "####" "#" 0 A_Overlay(SPower7,"BFR");
	  Stop;
   BFRLower:
	  "####" "#" 0 {
			 A_Overlay(FistsL,"FistsLeftReady");
			A_Overlay(FistsR,"FistsRightReady");
			A_TakeInventory("SPInUse",9999);
			A_TakeInventory("SP7InUse",9999);
	  }
	  Stop;
   }
}

Class PPGFistsUpgrade : CustomInventory
{
	Default {
		Inventory.PickupMessage "You got Enhanced Fists!";
		Inventory.PickupSound "weapon/pickup";
		+FLOATBOB
		RenderStyle "Add";
	}
	//Scale .75
	States
	{
	Spawn:
		SPWR A -1;
		Stop;
	Pickup:
		TNT1 A 0 A_JumpIfInventory("Backpack",1,2);
		TNT1 A 0 A_JumpIfInventory("PPGEnergy",300,"PickupFail");
		TNT1 A 0 A_JumpIfInventory("PPGEnergy",600,"PickupFail");
	PickupTrue:
		TNT1 A 0 A_JumpIfInventory("PPGUpgradedFists",1,2);
		TNT1 A 0 A_GiveInventory("PPGUpgradedFists");
		TNT1 A 0 A_GiveInventory("PPGEnergy",20);
		TNT1 A 0 A_KillMaster;
		Stop;
	PickupFail:
		TNT1 A 0;
		fail;
	}
}

Class PPGFistsPunchLeft : Inventory
{ Default { +INVENTORY.UNDROPPABLE } }

Class PPGUpgradedFists : Inventory
{ Default { +INVENTORY.UNDROPPABLE } }

Class RHSeq : Inventory
{
	Default {
		Inventory.MaxAmount 1;
		-INVENTORY.INVBAR
	}
}
/*
Class FistPuff : Actor
{
	Default {
		+NOBLOCKMAP
		+NOGRAVITY
		+PUFFONACTORS
		DamageType "Melee";
		activesound "";
		attacksound "fists/wall";
		seesound "fists/punch2";
	}
  states
  {
  Spawn:
    TNT1 A 0;
	TNT1 A 1;
	stop;
  Crash:
  	TNT1 A 0;
	TNT1 A 1;
  	stop;
  Melee:
  	TNT1 A 0;
	TNT1 A 1;
	stop;
  }
}
*/
Class FistPuff2 : Actor
{
	Default {
		+NOBLOCKMAP
		+NOGRAVITY
		+PUFFONACTORS
		DamageType "Melee";
		activesound "";
		attacksound "fists/wall";
		seesound "fists/punch";
	}
  states
  {
  Spawn:
    TNT1 A 0;
	TNT1 A 1;
	stop;
  Crash:
  	TNT1 A 0;
	TNT1 A 1;
  	stop;
  Melee:
  	TNT1 A 0;
	TNT1 A 1;
	stop;
  }
}

Class RHPuff : Actor
{
	Default {
		+NOBLOCKMAP
		+NOGRAVITY
		+PUFFONACTORS
		+PUFFGETSOWNER
		DamageType "Melee";
		activesound "";
		attacksound "fists/wall";
		seesound "roundhouse/kick";
	}
  states
  {
  Spawn:
    TNT1 A 0;
	TNT1 A 1;
	stop;
  Crash:
  	TNT1 A 0;
	TNT1 A 1;
  	stop;
  Melee:
  	TNT1 A 0;
	TNT1 A 1 A_RadiusThrust(5120,4,0);
	stop;
  }
}
